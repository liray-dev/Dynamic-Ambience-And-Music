buildscript {
    repositories {
        mavenCentral()
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = "https://repo.spongepowered.org/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:5.1.40'
        classpath "org.spongepowered:mixingradle:0.7-SNAPSHOT"
    }
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle'
apply plugin: "org.spongepowered.mixin"

idea {
    module {
        inheritOutputDirs = true
    }
}

version = '1.3.0'
group = 'daam'
archivesBaseName = 'Dynamic-Ambience-And-Music'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: 'stable', version: '39-1.12'

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }

        server {
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }
    }
}

mixin {
    add sourceSets.main, "mixins.daam.refmap.json"
}

repositories {
    mavenCentral()
    maven { url = "https://repo.spongepowered.org/maven" }
    maven { url = "https://jitpack.io" }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2860'

    implementation "org.spongepowered:mixin:0.8.3-SNAPSHOT"
    compileOnly 'org.projectlombok:lombok:1.18.30'

    implementation 'com.github.Querz:nbt:6.1'

    annotationProcessor 'org.spongepowered:mixin:0.8.3-SNAPSHOT:processor'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    compileOnly fileTree(dir: 'optifine', include: '*.jar')
    compileOnly fileTree(dir: 'libs', include: '*.jar')
}

compileJava.options.encoding = 'UTF-8'

jar {
    manifest {
        attributes([
            "Specification-Title": "Dynamic-Ambience-And-Music",
            "Specification-Vendor": "Dynamic-Ambience-And-Music",
            "Specification-Version": "1",
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" :"Dynamic-Ambience-And-Music",
            "FMLAT": "accesstransformer.cfg",
            "FMLCorePluginContainsFMLMod": "true",
            "ForceLoadAsMod": "true",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar.finalizedBy('reobfJar')